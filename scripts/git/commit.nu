export-env {
  if ($env.GIT_COMMIT_TYPE? | is-empty) {
    $env.GIT_COMMIT_TYPE = [
      [value    description];
      [feat     'æ–°å¢åŠŸèƒ½ | æ–°å¢äº†ä¸€ä¸ªåŠŸèƒ½']
      [fix      'ä¿®å¤ç¼ºé™· | ä¿®å¤äº†ä¸€ä¸ª Bug']
      [docs     'æ–‡æ¡£æ›´æ–° | ä¿®æ”¹æ–‡æ¡£ï¼Œä¾‹å¦‚ä¿®æ”¹ README æ–‡ä»¶ã€API æ–‡æ¡£ç­‰']
      [style    'ä»£ç æ ¼å¼ | ä¿®æ”¹ä»£ç çš„æ ·å¼ï¼Œä¾‹å¦‚è°ƒæ•´ç¼©è¿›ã€ç©ºæ ¼ã€ç©ºè¡Œç­‰']
      [refactor 'ä»£ç é‡æ„ | é‡æ„ä»£ç ï¼Œä¾‹å¦‚ä¿®æ”¹ä»£ç ç»“æ„ã€å˜é‡åã€å‡½æ•°åç­‰ä½†ä¸ä¿®æ”¹åŠŸèƒ½é€»è¾‘']
      [perf     'æ€§èƒ½æå‡ | ä¼˜åŒ–æ€§èƒ½ï¼Œä¾‹å¦‚æå‡ä»£ç çš„æ€§èƒ½ã€å‡å°‘å†…å­˜å ç”¨ç­‰']
      [test     'æµ‹è¯•ç›¸å…³ | ä¿®æ”¹æµ‹è¯•ç”¨ä¾‹ï¼Œä¾‹å¦‚æ·»åŠ ã€åˆ é™¤ã€ä¿®æ”¹ä»£ç çš„æµ‹è¯•ç”¨ä¾‹ç­‰']
      [build    'æ„å»ºç›¸å…³ | ä¿®æ”¹é¡¹ç›®æ„å»ºç³»ç»Ÿï¼Œä¾‹å¦‚ä¿®æ”¹ä¾èµ–åº“ã€å¤–éƒ¨æ¥å£æˆ–è€…å‡çº§ Node ç‰ˆæœ¬ç­‰']
      [ci       'æŒç»­é›†æˆ | æ›´æ”¹ CI é…ç½®æ–‡ä»¶æˆ–è„šæœ¬']
      [revert   'å›é€€ä»£ç  | å›é€€ä¸€ä¸ªæˆ–å¤šä¸ªæ›´æ”¹']
      [chore    'éä¸šåŠ¡æ€§ | å¯¹éä¸šåŠ¡æ€§ä»£ç è¿›è¡Œä¿®æ”¹ï¼Œä¾‹å¦‚ä¿®æ”¹æ„å»ºæµç¨‹æˆ–è€…å·¥å…·é…ç½®ç­‰']
      [misc     'æš‚æœªå½’ç±» | ä¸€äº›æœªåˆ†ç±»æˆ–ä¸çŸ¥é“å¦‚ä½•åˆ†ç±»çš„æ›´æ”¹']
    ]
  }

  if ($env.GIT_COMMIT_EMOJI? | is-empty) {
    $env.GIT_COMMIT_EMOJI = {
      feat: 'âœ¨'
      fix: 'ğŸ›'
      docs: 'ğŸ“š'
      style: 'ğŸ’'
      refactor: 'ğŸ“¦'
      perf: 'ğŸš€'
      test: 'ğŸš¨'
      build: 'ğŸ› '
      ci: 'ğŸ‘·'
      revert: 'âª'
      chore: 'ğŸ”§'
      misc: 'ğŸ§©'
    }
  }

  if ($env.GIT_USE_EMOJI? | is-empty) {
    $env.GIT_USE_EMOJI = false
  }

  if ($env.GIT_EMOJI_ALIGN? | is-empty) {
    $env.GIT_EMOJI_ALIGN = 'center'
  }
}

const GIT_SCOPES_KEY = 'nushell.scopes'
const SEPARATOR = 'Â»Â¦Â«'

def cmpl-types [] {
  $env.GIT_COMMIT_TYPE
}

def cmpl-scopes [] : nothing -> list<string> {
  ^git config get --local --default='' $GIT_SCOPES_KEY | split row $SEPARATOR
}

def cmpl-emoji-aligns [] : nothing -> list<string> {
  ["left" "center" "right"]
}

def get-emoji-symbol [type: string] : nothing -> string {
  let emoji = ^git config get --local --default='' $'nushell.emoji.($type)'

  if ( $emoji | is-empty ) {
    print $'(ansi yellow)Warning: type not found in nushell.emoji.($type) config(ansi reset)'
    print $'(ansi yellow)You can set it by running: (ansi reset)'
    print $'(ansi yellow)(char tab)git config set --local nushell.emoji.($type) "your-emoji-symbol"(ansi reset)'
    $env.GIT_COMMIT_EMOJI | get $type
  } else {
    $emoji
  }
}

def check-use-emoji [] : nothing -> bool {
  # ä¼˜å…ˆçº§: å½“å‰ä»“åº“gitconfig > ç¯å¢ƒå˜é‡
  let emoji = ^git config get --local --default='' 'nushell.useEmoji'
  if ($emoji == '') {
    $env.GIT_USE_EMOJI
  } else {
    $emoji | into bool
  }
}

def get-emoji-align [] : nothing -> string {
  # ä¼˜å…ˆçº§: å½“å‰ä»“åº“gitconfig > ç¯å¢ƒå˜é‡
  let align = ^git config get --local --default='' 'nushell.emojiAlign'
  if ($align == '') {
    $env.GIT_EMOJI_ALIGN
  } else {
    $align
  }
}

# æäº¤ä¿®æ”¹èŒƒå›´
export def git-commit-scope [
  --set(-s): list<string>
] : nothing -> list<string> {
  if ($set != null) {
    ^git config set --local $GIT_SCOPES_KEY ($set | str join $SEPARATOR)
  }
  ^git config get --local --default='' $GIT_SCOPES_KEY | split row $SEPARATOR
}

# å®ç°è§„èŒƒåŒ–æäº¤ä¿¡æ¯çš„åŠŸèƒ½
export def git-commit [
  subject_message: string                 # æäº¤æ ‡é¢˜æè¿°
  body?: string                           # æäº¤æè¿°
  --type(-t): string@cmpl-types           # æŒ‡å®šæäº¤ç±»å‹
  --scope(-s): string@cmpl-scopes         # æŒ‡å®šæäº¤èŒƒå›´
  --emoji(-e)                             # ä½¿ç”¨è¡¨æƒ…ç¬¦å·
  --no-emoji                              # ä¸ä½¿ç”¨è¡¨æƒ…ç¬¦å·
  --emoji-align: string@cmpl-emoji-aligns # è¡¨æƒ…ç¬¦å·å¯¹é½æ–¹å¼ (<left> type(scope): <center> subject <right>)

  --all(-A)                               # è‡ªåŠ¨æš‚å­˜æ‰€æœ‰å·²ä¿®æ”¹å’Œå·²åˆ é™¤çš„æ–‡ä»¶
  --amend(-a)                             # ä¿®æ”¹ä¸Šä¸€æ¬¡æäº¤è€Œä¸æ˜¯æ·»åŠ æ–°æäº¤
  --allow-empty                           # å…è®¸æ²¡æœ‰æ›´æ”¹çš„æäº¤
  --allow-empty-message                   # å…è®¸ç©ºæ¶ˆæ¯çš„æäº¤
  --gpg-sign                              # GPG ç­¾ç½²æäº¤
  --no-gpg-sign                           # ä¸ GPG ç­¾ç½²æäº¤
  --no-edit                               # ä¸ç¼–è¾‘æäº¤ä¿¡æ¯
  --author: string                        # è¦†ç›–æäº¤ä½œè€…
  --date: string                          # è¦†ç›–ä½œè€…æ—¥æœŸ

] : nothing -> nothing {
  let emoji_align = if ($emoji_align | is-empty) { get-emoji-align } else { $emoji_align }

  mut subject = []

  if ((not $no_emoji) and ($emoji or (check-use-emoji)) and ($emoji_align == 'left')) { $subject ++= [ (get-emoji-symbol $type) ] }

  if ($scope | is-not-empty) {
    if ($type | is-empty) {
      print $'(ansi red)Error: scope requires a type(ansi reset)'
      return
    }
    $subject ++= [ ($type + "(" + $scope + "):") ]
  } else if ($type | is-not-empty) {
    $subject ++= [ $'($type):' ]
  }

  if ((not $no_emoji) and ($emoji or (check-use-emoji)) and ($emoji_align == 'center')) { $subject ++= [ (get-emoji-symbol $type) ] }

  $subject ++= [ ($subject_message | str trim) ]

  if ((not $no_emoji) and ($emoji or (check-use-emoji)) and ($emoji_align == 'right')) { $subject ++= [ (get-emoji-symbol $type) ] }

  mut message = [ ($subject | str join ' ') ]

  if ($body | is-not-empty) { $message ++= [ ($body | str trim) ] }

  # skip: footer

  let message = ($message | str join $'(char newline)(char newline)')

  print $message

  mut args = [ --message $message ]
  if $all { $args ++= [ --all ] }
  if $amend { $args ++= [ --amend ] }
  if $allow_empty { $args ++= [ --allow-empty ] }
  if $allow_empty_message { $args ++= [ --allow-empty-message ] }
  if $gpg_sign { $args ++= [ --gpg-sign ] }
  if $no_gpg_sign { $args ++= [ --no-gpg-sign ] }
  if $no_edit { $args ++= [ --no-edit ] }
  if ($author | is-not-empty) { $args ++= [ --author $author ] }
  if ($date | is-not-empty) { $args ++= [ --date $date ] }

  ^git commit ...$args
}
